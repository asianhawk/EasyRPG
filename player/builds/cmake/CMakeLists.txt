CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

PROJECT(EasyRPG_Player CXX C)

# reader
ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/../../../readers/builds/cmake ${CMAKE_CURRENT_SOURCE_DIR}/../../../readers/builds/cmake)

SET(EXECUTABLE_OUTPUT_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../../bin/testgame)
SET(LIBRARY_OUTPUT_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../../lib)
LINK_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/../../lib)

# FIND_PACKAGE(Boost COMPONENTS serialization REQUIRED)
FIND_PACKAGE(Freetype REQUIRED)
FIND_PACKAGE(JPEG REQUIRED)
FIND_PACKAGE(PNG REQUIRED)
FIND_PACKAGE(SDL REQUIRED)
FIND_PACKAGE(SDL_image REQUIRED)
FIND_PACKAGE(SDL_mixer REQUIRED)
FIND_PACKAGE(SDL_ttf REQUIRED)
FIND_PACKAGE(ZLIB REQUIRED)

# SET(USE_OPENAL 1)

ADD_DEFINITIONS(
	${PNG_DEFINITIONS}

	-D UNIX=1
	-D USE_SDL=1
	-D USE_PIXMAN_BITMAP=1
)

IF(NOT CMAKE_GENERATOR MATCHES "Xcode")
	ADD_DEFINITIONS(
		-Wall -Werror # -Wextra
		-Wno-mismatched-tags
		-fno-exceptions -fno-rtti
	)
ENDIF()

IF(USE_OPENAL)
	FIND_PACKAGE(OpenAL REQUIRED)
	INCLUDE_DIRECTORIES(${OPENAL_INCLUDE_DIR})
	ADD_DEFINITIONS(-D USE_OPENAL=1)
ENDIF()

# endianess check
INCLUDE(TestBigEndian)
TEST_BIG_ENDIAN(IS_BIG_ENDIAN)
IF(IS_BIG_ENDIAN)
	ADD_DEFINITIONS(-D READER_BIG_ENDIAN=1)
ENDIF()

IF(APPLE)
	LINK_DIRECTORIES(/opt/local/lib)
	ADD_DEFINITIONS(-D COCOA=1)
ENDIF()

IF(CMAKE_BUILD_TYPE MATCHES "Debug")
	ADD_DEFINITIONS(-D _DEBUG=1)
ENDIF()

IF(CMAKE_SYSTEM_NAME MATCHES "Linux")
	ADD_DEFINITIONS(-D GTK=1)
ENDIF()

# include directory
INCLUDE_DIRECTORIES(
	# ${Boost_INCLUDE_DIRS}
	${FREETYPE_INCLUDE_DIRS}
	${JPEG_INCLUDE_DIR}
	${PNG_INCLUDE_DIR}
	${SDL_INCLUDE_DIR}
	${SDLIMAGE_INCLUDE_DIR}
	${SDLMIXER_INCLUDE_DIR}
	${SDLTTF_INCLUDE_DIR}
	${ZLIB_INCLUDE_DIRS}

	${CMAKE_CURRENT_SOURCE_DIR}/../../../readers/include
	/opt/local/include/pixman-1
	/usr/include/pixman-1
	/usr/local/include/pixman-1
	/usr/X11/include
)

# library
SET(EASYRPG_PLAYER_LIBRARIES
	# ${Boost_LIBRARIES}
	${FREETYPE_LIBRARIES}
	${JPEG_LIBRARIES}
	${PNG_LIBRARIES}
	${SDL_LIBRARY}
	${SDLIMAGE_LIBRARY}
	${SDLMIXER_LIBRARY}
	${SDLTTF_LIBRARY}
	${ZLIB_LIBRARIES}

	EasyRPG_Reader
	EasyRPG_Player_Static

	expat
	pixman-1
)
IF(USE_OPENAL)
	LIST(APPEND EASYRPG_PLAYER_LIBRARIES
		avformat avcodec avutil avdevice
		${OPENAL_LIBRARY}
	)
ENDIF()
IF(APPLE)
	LIST(APPEND EASYRPG_PLAYER_LIBRARIES iconv)
ENDIF()

# entry point
SET(MAIN_FILE "${CMAKE_CURRENT_SOURCE_DIR}/../../src/main.cpp")

# static library
AUX_SOURCE_DIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/../../src SRCS)
LIST(REMOVE_ITEM SRCS ${MAIN_FILE})
ADD_LIBRARY(EasyRPG_Player_Static STATIC ${SRCS})

ADD_EXECUTABLE(${PROJECT_NAME} MACOSX_BUNDLE ${CMAKE_CURRENT_SOURCE_DIR}/../../src/main.cpp)
TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${EASYRPG_PLAYER_LIBRARIES})

ADD_DEPENDENCIES(${PROJECT_NAME} EasyRPG_Reader EasyRPG_Player_Static)

# Doxygen
FIND_PACKAGE(Doxygen REQUIRED)
ADD_CUSTOM_TARGET(player_doc
	${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/../Doxyfile
	DEPENDS ${SRCS} ${MAIN_FILE}
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/..
	COMMENT "Generating API documentation with Doxygen" VERBATIM
)
ADD_CUSTOM_TARGET(doc)
ADD_DEPENDENCIES(doc player_doc reader_doc)

# test
ENABLE_TESTING()

ADD_LIBRARY(sdl_test_main STATIC ${CMAKE_CURRENT_SOURCE_DIR}/sdl_test_main.cpp)

INCLUDE(FindThreads REQUIRED)
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/../../src)

FUNCTION(CXX_TEST target libs)
	ADD_EXECUTABLE(test_${target} ${CMAKE_CURRENT_SOURCE_DIR}/../../test/${target}.cpp)
	TARGET_LINK_LIBRARIES(test_${target}
		${EASYRPG_PLAYER_LIBRARIES}
		${libs}
		"gtest"
	)
	IF(${UNIX})
		TARGET_LINK_LIBRARIES(test_${target} ${CMAKE_THREAD_LIBS_INIT})
	ENDIF()
	ADD_TEST(
		NAME test_${target}
		WORKING_DIRECTORY ${EXECUTABLE_OUTPUT_PATH}
		COMMAND ${EXECUTABLE_OUTPUT_PATH}/test_${target}
	)
	ADD_DEPENDENCIES(test_${target} ${PROJECT_NAME})
ENDFUNCTION()

SET(CXX_TEST_SOURCES "filefinder;utils;")
FOREACH(i ${CXX_TEST_SOURCES})
	CXX_TEST(${i} "sdl_test_main")
ENDFOREACH()
